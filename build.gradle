plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'java'
}

import org.apache.tools.ant.filters.ReplaceTokens

group 'kr.rvs.mclibrary'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

test.testLogging.showStandardStreams = true
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
compileJava.dependsOn clean

repositories {
    mavenCentral()
    /* Spigot Repo */
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    /* Bungeecord */
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    /* Vault Repo */
    maven { url 'http://nexus.hc.to/content/repositories/pub_releases' }
    /* ProtocolLib */
    maven { url 'http://repo.dmulloy2.net/content/groups/public/' }
    /* bStats */
    maven { url 'http://repo.bstats.org/content/repositories/releases/'}
}

dependencies {
    compile(group: 'org.spigotmc', name: 'spigot-api', version: '1.12.2-R0.1-SNAPSHOT') {
        exclude module: 'gson'
    }
    compile(group: 'net.milkbowl.vault', name: 'VaultAPI', version: '1.6') {
        transitive = false
    }
    compile(group: 'com.comphenix.protocol', name: 'ProtocolLib-API', version: '4.4.0-SNAPSHOT') {
        transitive = false
    }
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'org.bstats', name: 'bstats-bukkit', version: '1.2'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile fileTree(dir: 'libs', include: '*.jar')

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.0'
}

processResources {
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            "version"    : project.version,
            "pluginName" : rootProject.name,
            "mainPackage": project.group,
            "author"     : "EntryPoint"
    ]
}

shadowJar {
    classifier = null
    dependencies {
        include dependency('org.bstats:bstats-bukkit:1.2')
        include dependency('net.md-5:bungeecord-chat:1.12-SNAPSHOT')
        include dependency('com.google.code.gson:gson:2.8.1')
        include dependency('commons-io:commons-io:2.6')
    }
    exclude 'mojang-translations/*'
    relocate 'org.bstats.bukkit', 'kr.rvs.mclibrary.libs.metrics'
    relocate 'com.google.gson', 'kr.rvs.mclibrary.libs.gson'
    relocate 'org.apache.commons.io', 'kr.rvs.mclibrary.libs.apache.commons.io'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives shadowJar
    archives sourcesJar
    archives javadocJar
}
